# Tizen spec for Tizen 2.* Mobile,it is used for cross compilation using public
# rootstrap privided together with Tizen Native API in Tizen SDK.
# Depending on environment variables make it possible to change Tizen SDK path
# without rebuilding Qt

include(../../common/linux-g++-tizen.conf)

TIZEN_SDK_ROOT = $$(TIZEN_SDK_ROOT)
TIZEN_TOOLCHAIN_PREFIX = $$(TIZEN_TOOLCHAIN_PREFIX)
TIZEN_TOOLCHAIN_VERSION = $$(TIZEN_TOOLCHAIN_VERSION)
TIZEN_SYSROOT = $$(TIZEN_SYSROOT)

isEmpty(TIZEN_SYSROOT) || isEmpty(TIZEN_SDK_ROOT) || isEmpty(TIZEN_TOOLCHAIN_PREFIX) || isEmpty(TIZEN_TOOLCHAIN_VERSION) {
    error("One of TIZEN_SDK_ROOT, TIZEN_TOOLCHAIN_PREFIX, TIZEN_TOOLCHAIN_VERSION, TIZEN_SYSROOT is not defined. Please define corresponding env variables")
}

CROSS_COMPILE = $$TIZEN_SDK_ROOT/tools/$$TIZEN_TOOLCHAIN_PREFIX-gcc-$$TIZEN_TOOLCHAIN_VERSION/bin/$$TIZEN_TOOLCHAIN_PREFIX-

# modifications to g++-unix.conf
QMAKE_CC                = $${CROSS_COMPILE}gcc
QMAKE_CXX               = $${CROSS_COMPILE}g++
QMAKE_LINK              = $${QMAKE_CXX}
QMAKE_LINK_SHLIB        = $${QMAKE_CXX}

# modifications to linux.conf
QMAKE_AR                = $${CROSS_COMPILE}ar cqs
QMAKE_OBJCOPY           = $${CROSS_COMPILE}objcopy
QMAKE_NM                = $${CROSS_COMPILE}nm -P
QMAKE_STRIP             = $${CROSS_COMPILE}strip

# modifications to parameters set by linux_device_pre.conf
QMAKE_LFLAGS += --sysroot=$$TIZEN_SYSROOT
QMAKE_CFLAGS += --sysroot=$$TIZEN_SYSROOT
QMAKE_CXXFLAGS += --sysroot=$$TIZEN_SYSROOT

# TIZEN_ADDITIONAL_INCLUDES and TIZEN_ADDITIONAL_LIBS may be undefined
QMAKE_INCDIR += \
     $$TIZEN_SYSROOT/usr/include \
     $$TIZEN_SYSROOT/usr/include/dbus-1.0 \
     $$TIZEN_SYSROOT/usr/include/fontconfig \
     $$TIZEN_SYSROOT/usr/include/freetype2 \
     $$TIZEN_SYSROOT/usr/include/appfw \
     $$TIZEN_SYSROOT/usr/lib/dbus-1.0/include \
     $$TIZEN_SYSROOT/usr/include/appfw \
     $$TIZEN_SYSROOT/usr/include/system \
     $$TIZEN_SYSROOT/usr/include/dlog \
     $$TIZEN_ADDITIONAL_INCLUDES

QMAKE_LIBDIR += \
     $$TIZEN_SYSROOT/lib \
     $$TIZEN_SYSROOT/usr/lib \
     $$TIZEN_ADDITIONAL_LIBS

load(qt_config)
