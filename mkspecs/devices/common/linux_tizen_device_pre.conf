#tizen common mkspec

TIZEN_SDK_ROOT = $$(TIZEN_SDK_ROOT)
TIZEN_TOOLCHAIN_PREFIX = $$(TIZEN_TOOLCHAIN_PREFIX)
TIZEN_TOOLCHAIN_VERSION = $$(TIZEN_TOOLCHAIN_VERSION)
TIZEN_SYSROOT = $$(TIZEN_SYSROOT)

!isEmpty(TIZEN_SDK_ROOT):!isEmpty(TIZEN_TOOLCHAIN_PREFIX):!isEmpty(TIZEN_TOOLCHAIN_VERSION) {

    isEmpty(TIZEN_SYSROOT): error("Tizen Compiler defined but no Tizen sysroot defined. Please define Tizen sysroot in TIZEN_SYSROOT env variable")

    CROSS_COMPILE = $$TIZEN_SDK_ROOT/tools/$$TIZEN_TOOLCHAIN_PREFIX-gcc-$$TIZEN_TOOLCHAIN_VERSION/bin/$$TIZEN_TOOLCHAIN_PREFIX-
}

include(linux_device_pre.conf)

!isEmpty(TIZEN_SDK_ROOT):!isEmpty(TIZEN_TOOLCHAIN_PREFIX):!isEmpty(TIZEN_TOOLCHAIN_VERSION) {

    # modifications to parameters set by linux_device_pre.conf
    QMAKE_LFLAGS += --sysroot=$$TIZEN_SYSROOT
    QMAKE_CFLAGS += --sysroot=$$TIZEN_SYSROOT
    QMAKE_CXXFLAGS += --sysroot=$$TIZEN_SYSROOT

    # TIZEN_ADDITIONAL_INCLUDES and TIZEN_ADDITIONAL_LIBS may be undefined
    QMAKE_INCDIR += \
         $$TIZEN_SYSROOT/usr/include \
         $$TIZEN_SYSROOT/usr/include/dbus-1.0 \
         $$TIZEN_SYSROOT/usr/include/fontconfig \
         $$TIZEN_SYSROOT/usr/include/freetype2 \
         $$TIZEN_SYSROOT/usr/include/appfw \
         $$TIZEN_SYSROOT/usr/lib/dbus-1.0/include \
         $$TIZEN_SYSROOT/usr/include/appfw \
         $$TIZEN_SYSROOT/usr/include/system \
         $$TIZEN_SYSROOT/usr/include/dlog \
         $$TIZEN_ADDITIONAL_INCLUDES

    QMAKE_LIBDIR += \
         $$TIZEN_SYSROOT/lib \
         $$TIZEN_SYSROOT/usr/lib \
         $$TIZEN_ADDITIONAL_LIBS
}

QMAKE_PLATFORM = tizen $$QMAKE_PLATFORM
DEFINES += Q_OS_LINUX_TIZEN